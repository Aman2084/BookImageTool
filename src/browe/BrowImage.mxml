<?xml version="1.0" encoding="utf-8"?>
<mx:Canvas xmlns:fx="http://ns.adobe.com/mxml/2009" 
		   xmlns:s="library://ns.adobe.com/flex/spark" 
		   xmlns:mx="library://ns.adobe.com/flex/mx"
		   horizontalScrollPolicy="off" verticalScrollPolicy="off"
		   backgroundAlpha="0.6" backgroundColor="0x000000"
		   width="100%" height="100%" 
		   click="onClick(event)"
		   resize="onResize(event)">
	
	<fx:Script>
		<![CDATA[
			import flash.utils.clearTimeout;
			import flash.utils.setTimeout;
			
			import mx.events.ResizeEvent;
			
			import utils.ZEvent;
			
			private static const Auto:String = "auto";
			private static const ImgSize:String = "imgSize";
			
			[Bindable]
			private var _bmd:BitmapData = null;
			[Bindable]
			private var _showDownload:Boolean = true;
			
			private var _zoomType:String = "auto"
			private var _functioId:uint = 0;
			private var _index:int;
			private var _rect_draw:Rectangle = new Rectangle();
			private var _arr:Array;
			private var _isMove:Boolean = false;
			
			private function onClick($e:MouseEvent):void{
				if(_isMove){
					_isMove = false;
					return;
				}
				var w:Number = this.width / 5;
				if(this.mouseX<w && _arr && _arr.length>0){
					_index--;
					updata();
				}else if(this.mouseX>(w*4) && _arr && _arr.length>0){
					_index++;
					updata();
				}else{
					_zoomType = _zoomType==Auto ? ImgSize : Auto;
					onResize();
				}
			}
			
			private function updata():void{
				if(_index<0){
					_index = _arr.length - 1;
				}else if(_index>=_arr.length){
					_index = 0;
				}
				_bmd = _arr[_index];
				var s:String = (_index+1).toString() + "/" + _arr.length;
				txt.text = s;
				txt.visible = true;
				clearTimeout(_functioId);
				onResize();
				_functioId = setTimeout(onTime , 2000);
			}
			
			private function onTime():void{
				txt.visible = false;
			}
			
			public function setData($arr:Array , $index:int):void{
				_index = $index;
				_arr = $arr;
				updata();
			}
			
			private function onBtn($e:MouseEvent):void{
				var s:String = ""
				var data:Object = null
				switch($e.currentTarget){
					case btn_close:
						$e.stopPropagation()
						s = ZEvent.Close;
						break
					case btn_download:
						s = ZEvent.DownLoad;
						data = new Object();
						data.bmd = _arr[_index];
						data.index = _index;
						break
				}
				if(s){
					var evt:ZEvent = new ZEvent(s , data);
					this.dispatchEvent(evt);
				}
			}
			
			private function onResize($e:ResizeEvent=null):void{
				if(!_bmd){
					return;
				}
				var w:Number = this.width;
				var h:Number = this.height;
				var wb:Number = _bmd.width;
				var hb:Number = _bmd.height;
				
				if(_zoomType==Auto){
					if(w<wb || h<hb){ 			//需要时缩放
						var sw:Number = w / wb;
						var sh:Number = h / hb;
						var s:Number = Math.min(sw , sh);
						wb *= s;
						hb *= s;
					}
					img.width = wb;
					img.height = hb;
					img.x = (w-wb)/2;
					img.y = (h-hb)/2;
				}else if(_zoomType==ImgSize){
					img.width = wb;
					img.height = hb;
					img.x = img.y = 0;
					if(wb<w){
						img.x = (w-wb)/2;
					}
					if(hb<h){
						img.y = (h-hb)/2;
					}
					
					if(w>wb || h>hb){
						_rect_draw.left = w-wb;
						_rect_draw.top = h-hb;
					}else{
						_rect_draw.left = 0;
						_rect_draw.top = 0;
					}
				}
			}
			
			private function onDown($e:MouseEvent):void{
				if(_zoomType==Auto){
					return
				}
				if(_rect_draw.width==0 && _rect_draw.height==0){
					return
				}
				img.startDrag(false , _rect_draw);
				stage.addEventListener(MouseEvent.MOUSE_UP , onUp);
				stage.addEventListener(MouseEvent.MOUSE_MOVE , onMove);
			}
			
			private function onMove($e:MouseEvent):void{
				_isMove = true;
			}
			
			private function onUp($e:MouseEvent):void{
				img.stopDrag();
				var  s:Stage = $e.currentTarget as Stage;
				s.removeEventListener(MouseEvent.MOUSE_UP , onUp);
				s.removeEventListener(MouseEvent.MOUSE_MOVE , onMove);
			}
			
			public function set showDownload($b:Boolean):void{
				_showDownload = $b
			}
			
		]]>
	</fx:Script>
	
	<s:Image id="img"
		source="{_bmd}" scaleMode="stretch" smooth="true"
		mouseDown="onDown(event)"/>
	
	<s:Label id="txt"
		x="{(this.width -txt.width)/2}" bottom="10" width="100" height="10" 
		textAlign="center" alpha="0.5"/>
	
	<s:Image id="btn_close" 
			 buttonMode="true" source="@Embed('assets/close.png')" smooth="true" 
			 width="30" height="30" right="10" y="10" click="onBtn(event)"/>
	
	<s:Image id="btn_download"
			 width="30" height="30" right="10" bottom="10" smooth="true"
			 source="@Embed('assets/download.png')" buttonMode="true" click="onBtn(event)"
			 visible="{_showDownload}"/>
</mx:Canvas>
